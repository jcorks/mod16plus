@:class = import(module:'Matte.Core.Class');
@:Mod16 = import(module:'Mod16Plus.Core');
@:Fetcher = Mod16.Cartridge.import(source:'fetcher');
@:EventSystem = import(module:'Matte.Core.EventSystem');

@:Rectangle = Mod16.Cartridge.subCartridge(name:'rectangle-renderer');





@:UI = class(
    define:::(this) {
        @children = [];
        @childRequests = [];
        @parent;
        @layer = 0;
        @x_ = 0;
        @y_ = 0;
        @height_ = 0;
        @width_ = 0;
        @shown = true;
        @enableBackground = true;
        @events = EventSystem.new();
        @isRoot = false;

        @background;
        @bgColor = [0, 0, 0];
        
        @:isRooted = ::{
            @p = this;
            return {:::} {
                forever ::{
                    when(p == empty) send(message:false);
                    when(p.isRoot) send(message:true);
                    p = p.parent;
                }
            };
        };

        @:readjustBackground ::{
            if (background)
                Rectangle.destroy(ref:background);
            background = empty;
            
            when(!shown) empty;
            when(!enableBackground) empty;
 
            background = Rectangle.create(
                x:x_,
                y:y_,
                w:width_,
                h:height_,
                layer,
                color:bgColor
            );
        };
        
        
        events.events = {
            onMove ::(detail) {
            
            },
            onShow ::(detail) {
                shown = true;
                readjustBackground();                      
                children->foreach(do:::(i, child) <- child.show());
            },
            
            onHide ::(detail) {
                shown = false;
                readjustBackground();
                children->foreach(do:::(i, child) <- child.hide());
            
            },
            
            onEnable ::(detail) {
                children->foreach(do:::(i, child) <- child.enable());
            },           
            
            onDisable ::(detail) {
                children->foreach(do:::(i, child) <- child.disable());            
            },
            
            onResize ::(detail) {
                readjustBackground();            
            },
            
            onChildCountChange ::(detail) {            

            },
            
            onMove ::(detail) {
                readjustBackground();            
            },
            
            onEnter ::(detail) {
            
            },
            
            
            onLeave ::(detail) {
            
            },
            
            onLayerChange ::(detail) {
                children->foreach(do:::(i, child) <- child.layer = this.layer + 1);
                readjustBackground();
            }
            
            
            
        };
    
    
    
        this.interface = {

            show :: {
                when(shown) empty;
                events.emit(event:'onShow');      
                shown = true;
            },
            
            events : {
                get::<- events
            },
            
            hide :: {
                when(!shown) empty;
                events.emit(event:'onHide');
                shown = false;
            },
            
            shown : {
                get ::<- shown
            },

            enable :: {
                events.emit(event:'onEnable'); 
            },

            disable :: {
                events.emit(event:'onDisable'); 
            },
            
            x : {
                get ::<- x_
            },
            
            y : {
                get ::<- y_
            },
            

            width : {
                get :: <- width_
            },


            height : {
                get :: <- height_
            },
            
            resize ::(width, height) {
                when(width == width_ && height == height_) empty;  
                width_ = width;
                height_ = height;  
                events.emit(event:'onResize');
            },
            
            
            move ::(x, y) {  
                when(x == x_ && y == y_) empty;
                x_ = x;
                y_ = y;              
                events.emit(event:'onMove');
            },
            
            
            children : {
                get ::<- [...children]
            },
            
            childrenSizeRequests : {
                get ::<- [...childRequests]
            },
            
            layer : {
                get ::<- layer,
                set ::(value) {
                    when(layer == value) empty;
                    layer = value;
                    events.emit(event:'onLayerChange');
                }
            },
            
            parent : {
                get ::<- parent,
                set ::(value) <- parent = value
            },

            addChild ::(child, sizeRequest) {
                child.parent = this;
                children->push(value:child);
                childRequests->push(value:sizeRequest);
                events.emit(event:'onChildCountChange');
                child.layer = this.layer + 1;
            },

            clearChildren :: {
                children->foreach(do:::(i, child) <- child.parent = empty);
                children = [];
                childRequests = [];
                events.emit(event:'onChildCountChange');
            },

            background : {
                set ::(value) {
                    bgColor = value;
                    readjustBackground();
                }
            },
            
            isRoot : {
                get ::<- isRoot,
                set ::(value => Boolean) <- isRoot = value	
            },

            
            enableBackground : {
                set::(value) {
                    enableBackground = value;
                    readjustBackground();
                }
            }   
            
            
        
        
        };
        this.background = '#111111';
    }
);

return UI;
