@:class = import(module:'Matte.Core.Class');
@:SES = import(module:'SES.Core');
@:Cartridge = SES.Cartridge;
@:Fetcher = Cartridge.import(source:'fetcher');
@:TextArea = Cartridge.subCartridge(name:'textarea');
@:UI = Cartridge.import(source:'ui');

return class(
    inherits:[UI],
    define:::(this) {
    
        
        @foregroundPalette = [1, 1, 1];
        @:textarea = TextArea.new();
        textarea. = {
            editable : false,
            heightChars: 1,
            layer : this.layer + 1
        };

        @text_ = '';
        
        @:readjust ::{
            textarea.widthChars = text_->length;        
            textarea.x = this.x + this.width / 2  - textarea.width  / 2;
            textarea.y = this.y + this.height / 2 - textarea.height / 2;                        
        };
        
        
        this.constructor = ::(text) {
            this.foreground = '#ffffff';
            textarea.defaultPalette = foregroundPalette;
            textarea.text = text;
            text_ = text;
            this.enableBackground = false;
            readjust();
            return this;
        };
        
        
        this.events.installHooks(
            events:{
                onShow ::(detail){
                    textarea.text =  text_;
                    readjust();
                },
                
                onHide ::(detail){
                    textarea.text = '';
                },
                
                onResize ::(detail) {
                    readjust();
                },
                
                onMove ::(detail) {
                    readjust();
                },
                
                onLayerChange::(detail) {
                    textarea.layer = this.layer + 1;
                }

            }
        );        
        
        
        this.interface = {
            foreground : {
                set ::(value => String) {
                    foregroundPalette = value;
                    textarea.defaultPalette = foregroundPalette;
                }
            },
            

            
            text : {
                set ::(value => String) {
                    text_ = value;
                    if (this.shown) ::<= {
                        textarea.text = value;
                        readjust();
                    };
                },
                
                get ::(value) {
                    return textarea.text;
                }
            }
        };
    }
);
