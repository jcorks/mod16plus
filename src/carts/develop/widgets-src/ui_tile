@:class = import(module:'Matte.Core.Class');
@:SES = import(module:'SES.Core');
@:Cartridge = SES.Cartridge;
@:Fetcher = Cartridge.import(source:'fetcher');
@:UI = Cartridge.import(source:'ui');
return class(
    inherits:[UI],
    define:::(this) {
    
        @:sprite = Fetcher.Sprite.newID();
        @:tile = Fetcher.Tile.newID();
        
        Cartridge.Sprite.set(
            index:sprite,
            tile
        );
        
        @resizeable = false;
        
        @:reposition :: {
            if (resizeable) ::<= {
                Cartridge.Sprite.set(
                    index:sprite,
                    x:this.x,
                    y:this.y
                );
                Cartridge.Sprite.transform(
                    index:sprite,
                    scaleX: this.width / 8,
                    scaleY: this.height / 8
                );

            } else 
                Cartridge.Sprite.set(
                    index:sprite,
                    x:this.x + this.width / 2 - 4,
                    y:this.y + this.height / 2 - 4
                )
            ;
        
        };
        this.events.installHooks(
            events:{
                onShow ::(detail){
                    Cartridge.Sprite.set(
                        index:sprite,
                        show: true
                    );
                },
                
                onHide ::(detail){
                    Cartridge.Sprite.set(
                        index:sprite,
                        show: false
                    );
                },
                
                onResize ::(detail) {
                    reposition();
                },
                
                onMove ::(detail) {
                    reposition();
                },
                
                onLayerChange::(detail) {
                    SES.Sprite.set(
                        index:sprite,
                        layer: this.layer
                    );
                }

            }
        );        
        
        this.constructor = ::(palette => Number, tileData) {
            Cartridge.Sprite.set(
                index:sprite,
                show:true,
                layer: this.layer,
                palette
            );  
            return this;
        };
        
        this.interface = {
            palette : {
                set ::(value => Number) {
                    SES.Sprite.set(
                        index:sprite,
                        palette: value
                    );  

                }
            },

            resizeable : {
                set ::(value => Boolean) {
                    resizeable = value;
                    reposition();
                }
            },
            
            tile : {
                set ::(value => Object) {
                    SES.Tile.set(
                        index:tile,
                        data: value
                    );
                }
            }
        };

        this.enableBackground = false;
    }
);
