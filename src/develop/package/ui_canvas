@:class = import(module:'Matte.Core.Class');
@:UI = import(module:'ui');
@:TextArea = import(module:'textarea');
@:Fetcher = import(module:'fetcher');
@:SES = import(module:'SES.Core');
@:Debug = import(module:'SES.Debug');
return class(
    inherits:[UI],
    define:::(this) {
    
        @:sprite = Fetcher.Sprite.newID();
        @tile;
        @pen = 1;
        @onDraw = ::{};
        @needsUpdate = false;
        
        SES.Sprite.set(
            index:sprite,
            tile:tile
        );  
        
        
        
        
        


        @size = 64;        
        @inputid;
        @down = false;
        @:connectInput = ::{ 
            when(inputid != empty) empty;  
            inputid = SES.Input.addCallback(
                device: SES.Input.DEVICES.POINTER0,
                callback ::(event, x, y, button) {
                
                    
                
                    match(event) {                    
                      (SES.Input.EVENTS.POINTER_BUTTON_DOWN):::<= {
                        down = true;
                      },
                      (SES.Input.EVENTS.POINTER_BUTTON_UP):::<= {
                        down = false;
                        if (needsUpdate) onDraw();
                        needsUpdate = false;
                      }
                    };
                    
                    
                    when(down) ::<= {
                        @:thisx = this.x + this.width / 2 - size / 2;
                        @:thisy = this.y + this.height / 2 - size / 2;




                        when (x < thisx || x > thisx + size || y < thisy || y > thisy + size) empty; 
                        
                        @:px = (((x - thisx) / size)*8)->floor;
                        @:py = (((y - thisy) / size)*8)->floor;
                        
                        
                        if (tile) ::<= {
                            @data = SES.Tile.get(index:tile);
                            @:index = (px + py * 8)->floor; 
                            if (data[index] != pen) ::<= {
                                data[index] = pen;
                                SES.Tile.set(index:tile, data);
                                needsUpdate = true;
                            };
                        };
                        
                        
                        print(message:''+px + ', ' + py);
                    
                    };
                }
            );
        };        
        
        connectInput();
        @:reposition :: {
            SES.Sprite.set(
                index:sprite,
                x:this.x + this.width / 2 - size / 2,
                y:this.y + this.height / 2 - size / 2 
            );
            SES.Sprite.transform(
                index:sprite,
                scaleX: size / 8,
                scaleY: size / 8
            );

        
        };
        this.events.installHooks(
            events:{
                onShow ::(detail){
                    SES.Sprite.set(
                        index:sprite,
                        show: true
                    );
                    connectInput();
                },
                
                onHide ::(detail){
                    SES.Sprite.set(
                        index:sprite,
                        show: false
                    );
                    if (inputid != empty) ::<= {
                        SES.Input.removeCallback(device:SES.Input.DEVICES.POINTER0, id:inputid);
                        inputid = empty;
                    };
                    down = false;

                },
                
                onResize ::(detail) {
                    reposition();
                },
                
                onMove ::(detail) {
                    reposition();
                },
                
                onLayerChange::(detail) {
                    SES.Sprite.set(
                        index:sprite,
                        layer: this.layer
                    );
                }

            }
        );        
        
        this.constructor = ::() {
            SES.Sprite.set(
                index:sprite,
                show:true,
                layer: this.layer
            );  
            return this;
        };
        
        this.interface = {
            palette : {
                set ::(value => Number) {
                    SES.Sprite.set(
                        index:sprite,
                        palette: value
                    );  
                }
            },

            tile : {
                set ::(value => Number) {
                    tile = value;
                    SES.Sprite.set(
                        index:sprite,
                        tile: value
                    );  
                }
            },


            onDraw : {
                set ::(value => Function) {
                    onDraw = value;
                }
                
            },
            
            pen : {
                set ::(value => Number) {
                    pen = value;
                }
            },

            size : {
                set ::(value => Number) {
                    size = value;
                    reposition();
                }
            }
            
        };

        this.enableBackground = false;
        //this.background = '#030303';
    }
);
