@:class = import(module:'Matte.Core.Class');
@:UI = import(module:'ui');
@:TextArea = import(module:'textarea');
@:Fetcher = import(module:'fetcher');
@:Debug = import(module:'SES.Debug');
@:SES = import(module:'SES.Core');
@:UILabel    = import(module:'ui_label');
@:UILayout   = import(module:'ui_layout');
@:UIFlat   = import(module:'ui_flat');
@:UIMousable   = import(module:'ui_mousable');



// it is basically a lyout of buttons with special background settings.
return class(
    inherits:[UI],
    define:::(this) {

        @layout = UILayout.new(children:[], enableBackground:false);
        @ticks = [];
        @choice = -1;
        @userChoose = ::(choice){};

        this.constructor = ::(choices, onChoose) {
            this.addChild(child:layout, sizeRequest:1);
            if (choices != empty) this.choices = choices;
            if (onChoose != empty) userChoose = onChoose;
            return this;
        };

        @:resetChild ::{
            layout.move(x: this.x, y: this.y);
            layout.resize(width:this.width, height:this.height);
        };

        this.events.installHooks(
            events: {
                onChildCountChange ::(detail){
                    resetChild();            
                },                            
                
                onResize ::(detail) {
                    resetChild();            
                },
                
                onMove ::(detail) {
                    resetChild();            
                }
            }
        );

        this.interface = {
            choices : {
                set ::(value => Object) {
                    layout.clearChildren();
                    
                    
                    @:set = [];
                    
                    value->foreach(do:::(i, v) {
                        @:tick = UILabel.new(text:''); 
                        @:child = UIFlat.new(
                            children: [
                                [tick, 0],
                                [v, 1]
                            ]
                        );
                        @:button = UIMousable.new(child);
                        ::(value){
                            @index = value;
                            button.onClick = ::{
                                this.choice = i;
                            };
                        }(value: i);
                        ticks->push(value:tick);
                        //layout.addChild(child:button, sizeRequest:1/value->keycount);
                        set->push(value:[button, 1/value->keycount]);
                    });
                    layout.reset(children:set);
                }
            },

            onChoose : {
                set ::(value) <- userChoose = value => Function
            },

            choice : {
                get ::<- choice,
                set ::(value => Number) {
                    when (value == choice) empty;
                    choice = value;
                    ticks->foreach(do:::(i, v) {
                        v.text = '';
                    });

                    when(choice < 0 || choice >= ticks->keycount) empty;
                    ticks[choice].text = '>';

                    userChoose(choice:value);
                }
            }

        };
    }
);

