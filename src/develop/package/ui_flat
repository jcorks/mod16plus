@:class = import(module:'Matte.Core.Class');
@:UI = import(module:'ui');
@:TextArea = import(module:'textarea');
@:Fetcher = import(module:'fetcher');
@:Debug = import(module:'SES.Debug');

return class(
    inherits:[UI],
    define:::(this) {
        @padding = 1;
        @:resetChildren ::{
            @:requests =  this.childrenSizeRequests;
            @last = 0;
            this.children->foreach(do:::(i, child) {
                child.move(y: padding + this.y, x: padding + this.x + last);
                child.resize(
                    height:this.height - padding*2,
                    width:requests[i]*this.width - padding*2
                );
                last += requests[i]*this.width;
            });
        };        
        
        this.constructor = ::(
            children => Object,
            enableBackground
        ) {
            if (enableBackground != empty) this.enableBackground = enableBackground;
            children->foreach(do:::(i, value){
                this.addChild(child:value[0] => Object, sizeRequest:value[1] => Number);
            });
            return this;
        };


        this.events.installHooks(
            events:{
                onAddChild ::(detail){
                    resetChildren();            
                },
                
                onResize ::(detail) {
                    resetChildren();            
                },
                
                onMove ::(detail) {
                    resetChildren();            
                }
            
            }
        );
        
        
        this.interface = {
            padding: {
                set ::(value => Number) {
                    padding = value;
                    resetChildren();
                }
            },
            
        
            
        };
    }
);
