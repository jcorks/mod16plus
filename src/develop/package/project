@:class = import(module:'Matte.Core.Class');
@:JSON = import(module:'Matte.Core.JSON');
@:Debug = import(module:'SES.Debug');
@:package_native__save_source  = getExternalFunction(name:"package_native__save_source");
@:package_native__make_project = getExternalFunction(name:"package_native__make_project");
@:package_native__open_source  = getExternalFunction(name:"package_native__open_source");
@:package_native__list_projects= getExternalFunction(name:"package_native__list_projects");

@:checkNameAllowed = ::(name) {
    when(name == '') false;
    return [::] {
        [0, name->length]->for(do:::(i) {
            match(name->charAt(index:i)) {
              (',', '.', '/', '\\', '\n', '\t'): 
                send(message:false)
            };
        });
        return true;
    };
};

return class(
    name: 'Project',
    statics : {
    },
    define:::(this) {
        // name of project. is not allowed to contain slashes or dots.
        @directory;

        // name of source. is not allowed to contain slashes or dots.
        @sources = [
            'main'
        ];
        
        // palette entries. Arrays of id followed by a path to a palette file (plaintext)
        @palettes = [];
    
        this.interface = {
            isNameAllowed ::(name) {
                return checkNameAllowed(name);
            },

            name : {
                get ::<- directory,
                set ::(value) {
                    package_native__make_project(a:value=>String);                    
                    directory = value;
                }
            },
        
        
            newPalette ::(id => Number) {
                // check if exists.
                palettes->foreach(do:::(index, value) {
                    when(value[0] == id) error(detail:'Palette already exists!');
                });
                
                palettes->push(value:[id, 'palette_'+id]);
                package_native__save_source(a:directory, b:'palette_'+id, c: '1 0 1  0 1 0  0 0 1  1 1 1');
            },
            
            savePalette ::(id => Number, paletteColors => Object) {
                @entry;
                when([::] {
                    palettes->foreach(do:::(index, value) {
                        when(value[0] == id) ::<= {
                            entry = value;
                            send(message:false);
                        };
                    });
                    return true;
                }) error(detail:'No such palette exists');

    
                package_native__save_source(a:directory, b:entry[1], c:
                    '' + 
                    paletteColors[0] + ' ' + paletteColors[1] + ' ' + paletteColors[2] + ' ' +
                    paletteColors[3] + ' ' + paletteColors[4] + ' ' + paletteColors[5] + ' ' +
                    paletteColors[6] + ' ' + paletteColors[7] + ' ' + paletteColors[8] + ' ' +
                    paletteColors[9] + ' ' + paletteColors[10] + ' ' + paletteColors[11]
                );
            },
            
            openPalette ::(id => Number) {
                @entry;
                when([::] {
                    palettes->foreach(do:::(index, value) {
                        when(value[0] == id) ::<= {
                            entry = value[1];
                            send(message:false);
                        };
                    });
                    return true;
                }) error(detail:'No such palette exists');
            
                @:out = [];
                @:text = package_native__open_source(a:directory, b:entry);
                text->split(token:' ')->foreach(
                    do:::(index, token) {
                        when(token == '') empty;
                        out->push(value:Number.parse(string:token));
                    }                   
                );
                return out;
            },
        
            removePalette ::(id => Number) {
                @entry;
                when([::] {
                    palettes->foreach(do:::(index, value) {
                        when(value[0] == id) ::<= {
                            entry = index;
                            send(message:false);
                        };
                    });
                    return true;
                }) error(detail:'No such palette exists');


                palettes->remove(key:entry);
            },        
        
            newSource ::(name) {
                when(!checkNameAllowed(name))
                    error(detail:'Invalid name.');
                sources->push(value:name);
                this.save();
                this.saveSource(name, data:'');
            },
        
            openSource ::(name) {
                return package_native__open_source(a:directory, b:name);            
            },
            

            saveSource ::(name, data => String){
                @:index = sources->findIndex(value:name);
                if (index < 0)
                    error(detail:'No such filename. Internal error');
                
                package_native__save_source(a:directory, b:name, c:data); 
            },

            removeSource ::(name) {
                sources->remove(key:sources->findIndex(value:name));
            },

            all : {
                get::<-package_native__list_projects()
            },

        
            sources : {
                get::<-[...sources]
            },
            
            
            palettes : {
                get::<- [...palettes]
            },

            save :: {
                @obj = {
                    sources: sources,
                    palettes: palettes
                };
                // make object for json
                // save project.json file

                package_native__save_source(a:directory, b:'project.json', c:JSON.encode(object:obj));
                // package command native function
            },
            
            load :: {
                @str = this.openSource(name:'project.json');
                @obj = JSON.decode(string:str);
                sources = obj.sources;
                palettes = obj.palettes;
            },
            
            package :: {
            
            }
        };
    }
).new();
